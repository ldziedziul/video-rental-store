buildscript {
    ext {
        springCloudContractVersion = "2.1.1.RELEASE"
    }
    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${springCloudContractVersion}"
    }
}
plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'java'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.3"
}
ext {
    spockVersion = "1.2-groovy-2.5"
    ioCucumberVersion = "4.2.6"
    springfoxVersion = "2.9.2"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'spring-cloud-contract'

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"
    }
}

group = 'pl.dziedziul'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation "org.spockframework:spock-spring:$spockVersion"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-verifier"
    testImplementation "io.cucumber:cucumber-java:$ioCucumberVersion"
    testImplementation "io.cucumber:cucumber-junit:$ioCucumberVersion"
    testImplementation "io.cucumber:cucumber-spring:$ioCucumberVersion"
    runtimeOnly "io.springfox:springfox-swagger-ui:${springfoxVersion}"
    implementation "io.springfox:springfox-swagger2:${springfoxVersion}"
}

contracts {
    testFramework = 'SPOCK'
    baseClassForTests = 'pl.dziedziul.videorentalstore.test.contract.BaseContractSpecification'
    basePackageForTests = 'pl.dziedziul.videorentalstore.test.contract'
}

cucumberReports {
    outputDir = file('build/reports/cucumber/')
    reports = files('build/reports/cucumber/cucumber-report.json')
}
test.outputs.file('build/reports/cucumber/cucumber-report.json')
